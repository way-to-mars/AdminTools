импорт Пользователи


@Обработчик
метод ПослеСоздания()
    ПодключитьОбработчикТаймера(() -> ПоискМетаданных(), 4мс, Истина)
;

@Локально
метод ПоискМетаданных()
    знч ВсеМетаданные = ОбъектМетаданных.ВсеМетаданные(ПосчитатьКоличество = Истина)
    этот.МассивМетаданных.Данные.ДобавитьВсе(ВсеМетаданные)
    ПодключитьОбработчикТаймера(() -> ЗагрузкаДополнительныхПараметров(), 4мс, Истина)
;

@Локально
метод ЗагрузкаДополнительныхПараметров()
    знч НовыеДанные = ЗаполнитьДопПараметры(МассивМетаданных.Данные)
    для Индекс = 0 по НовыеДанные.Граница()
        знч Прежнее = МассивМетаданных.Данные.Получить(Индекс)
        знч Новое = НовыеДанные.Получить(Индекс)
        Прежнее.ПомеченныхНаУдаление = Новое.ПомеченныхНаУдаление
        Прежнее.ПоследнийСсылка = Новое.ПоследнийСсылка
    ;
;

@Локально @НаСервере @ДоступноСКлиента
статический метод ЗаполнитьДопПараметры(ВсеМетаданные: Массив<ОбъектМетаданных>): Массив<ОбъектМетаданных>
    для Метаданные из ВсеМетаданные
        Метаданные.ПосчитатьПомеченныеНаУдаление()
        Метаданные.НайтиПоследнийЭлемент()
    ;
    возврат ВсеМетаданные
;

@Локально @НаСервере @ДоступноСКлиента
статический метод ОбновитьПараметры(Метаданные: ОбъектМетаданных): ОбъектМетаданных
    Метаданные.ПосчитатьПомеченныеНаУдаление()
    Метаданные.НайтиПоследнийЭлемент()
    Метаданные.ПосчитатьКоличествоЭлементов()
    возврат Метаданные
;

метод КликПоСсылке(Источник: СтандартнаяКолонкаТаблицы<ОбъектМетаданных>, Событие: СобытиеПриНажатии, ДанныеСтроки: ОбъектМетаданных)

    выбор ДанныеСтроки.Вид
    когда Справочник
        новый ФормаСправочника(ИсточникМетаданных = ДанныеСтроки).ОткрытьВМодальномОкне(ОжидатьЗакрытия = Истина)
    когда Документ
        новый ФормаДокумента(ИсточникМетаданных = ДанныеСтроки).ОткрытьВМодальномОкне(ОжидатьЗакрытия = Истина)
    когда ХранилищеНастроек
        новый ФормаХранилищаНастроек(ИсточникМетаданных = ДанныеСтроки).ОткрытьВМодальномОкне(ОжидатьЗакрытия = Истина)
    когда РегистрСведений
        новый ФормаРегистраСведений(ИсточникМетаданных = ДанныеСтроки).ОткрытьВМодальномОкне(ОжидатьЗакрытия = Истина)
    когда РегистрНакопления
        новый ФормаРегистраНакопления(ИсточникМетаданных = ДанныеСтроки).ОткрытьВМодальномОкне(ОжидатьЗакрытия = Истина)
    когда StdХранилищеНастроек
        новый СтандартноеХранилищеНастроек.АвтоматическаяФормаСписка().ОткрытьВМодальномОкне(ОжидатьЗакрытия = Истина)
    когда StdПользователи
        новый ФормаПользователи().ОткрытьВМодальномОкне(ОжидатьЗакрытия = Истина)
    иначе
        ВсплывающееСообщение(ДанныеСтроки.Название, ОценкаИнформации.Предупреждающая)
    ;
    
    ПодключитьОбработчикТаймера( () -> ОбновитьДанныеОбъекта(ДанныеСтроки), 4мс, Истина)
    
;

@Локально @НаКлиенте
метод ОбновитьДанныеОбъекта(ДанныеСтроки: ОбъектМетаданных)
    
    знч СтарыеДанные = этот.МассивМетаданных.Данные.Фильтровать( Мд -> Мд == ДанныеСтроки).ЕдинственныйИлиНеопределено()
    
    если СтарыеДанные != Неопределено
        знч НовыеДанные = ОбновитьПараметры(ДанныеСтроки)
        СтарыеДанные.ПомеченныхНаУдаление = НовыеДанные.ПомеченныхНаУдаление
        СтарыеДанные.ПоследнийСсылка = НовыеДанные.ПоследнийСсылка
        СтарыеДанные.КоличествоЭлементов = НовыеДанные.КоличествоЭлементов
    ;
;

метод ВсплывающееСообщение(Текст: Строка, ОценкаИнформации = ОценкаИнформации.Обычная)
    знч Уведм = новый Уведомление("", Текст)
    Уведм.Вид = ВидУведомления.Всплывающее
    Уведм.КлючУникальности = "ФормаМетаданные"
    Уведм.Таймаут = 2с
    Уведм.ОценкаИнформации = ОценкаИнформации
    Уведм.Показать()    
;

метод ОбработчикСтроки(Команда: КомандаСПараметром<ОбъектМетаданных>, Параметр: ОбъектМетаданных)
    знч СсылкаПоследнего = Параметр.ПоследнийСсылка
    знч Вид = Параметр.Вид    
    если СсылкаПоследнего != Неопределено и Вид!= Неопределено
        Сообщить(ЗагрузитьПоследний(СсылкаПоследнего, Вид))
    ;
;

@НаСервере @ДоступноСКлиента
статический метод ЗагрузитьПоследний(Ссылка: Сущность.Ключ, ВидМетаданных: ВидыМетаданных): Строка

    если ВидМетаданных == ВидыМетаданных.Справочник
        знч СправСсылка= Ссылка как Справочник.Ссылка
        знч Последний = СправСсылка.ЗагрузитьОбъект()
        знч Представление = Последний.Представление()
        возврат Представление
    ;    

    возврат "нетутъ"
;