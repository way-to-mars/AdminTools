импорт Пользователи


@Обработчик
метод ПослеСоздания()
    ПодключитьОбработчикТаймера(() -> ЗагрузитьСписок(), 4мс, Истина)
;

@НаКлиенте
метод ЗагрузитьСписок()
    если ИсточникМетаданных.Вид == ВидыМетаданных.StdХранилищеНастроек
        знч Элементы = ЭлементыХранилищаНастроек(ИсточникМетаданных)
        этот.МассивЭлементов.Данные = Элементы
        
        если Элементы.Пусто()
            знч КПТ = новый КомпонентПустаяТаблица(Сообщение = "В хранилище пусто")
            Компоненты.ГруппаОсновная.Содержимое.Добавить(КПТ)
        ;
    иначе
        Сообщить("Форма предназначена для отображения хранилища настроек.\нВид полученного объекта: %{ИсточникМетаданных.Вид}")
    ;
;

@НаСервере @ДоступноСКлиента
статический метод ЭлементыХранилищаНастроек(Метаданные: ОбъектМетаданных): Массив<СвойстваХранилищаНастроек>

    знч ТекстЗапроса =
       "ВЫБРАТЬ ПЕРВЫЕ 20
            МетаДанные.Ссылка.ЗаменитьNull(Неопределено) КАК Ссылка,
            МетаДанные.МеткаВерсии.ЗаменитьNull(Неопределено) КАК МеткаВерсии,
            МетаДанные.КлючОбъекта.ЗаменитьNull(Неопределено)КАК КлючОбъекта,
            МетаДанные.КлючНастройки.ЗаменитьNull(Неопределено) КАК КлючНастройки,
            МетаДанные.Пользователь.ЗаменитьNull(Неопределено) КАК Пользователь,
            МетаДанные.Вариант.ЗаменитьNull(Неопределено) КАК Вариант,
            МетаДанные.Значение.ЗаменитьNull(Неопределено) КАК Значение,
            МетаДанные.Общая.ЗаменитьNull(Неопределено) КАК Общая
        ИЗ
            %{Метаданные.ТипОбъекта} как МетаДанные
        УПОРЯДОЧИТЬ ПО
            МетаДанные.Ссылка УБЫВ               
        "
    исп РезультатЗапроса = новый ПроизвольныйЗапрос(ТекстЗапроса).Выполнить()

    возврат РезультатЗапроса.Преобразовать(СРПЗ -> новый СвойстваХранилищаНастроек(
            Ссылка = СРПЗ.Ссылка,
            КлючОбъекта = СРПЗ.КлючОбъекта,
            КлючНастройки = СРПЗ.КлючНастройки,
            Значение = СРПЗ.Значение,
            Общая = СРПЗ.Общая,
            Пользователь = СРПЗ.Пользователь,
            МеткаВерсии = СРПЗ.МеткаВерсии,
            Вариант = СРПЗ.Вариант))
;

метод ПриВыбореПользователя(Источник: СтандартнаяКолонкаТаблицы<СвойстваХранилищаНастроек>, Событие: СобытиеПриНажатии, ДанныеСтроки: СвойстваХранилищаНастроек)
    новый ФормаПользователи().ОткрытьВМодальномОкне(ОжидатьЗакрытия = Истина)
;