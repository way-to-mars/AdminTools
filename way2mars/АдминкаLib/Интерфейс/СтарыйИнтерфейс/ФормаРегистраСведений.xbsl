@Обработчик
метод ПослеСоздания()
    ПодключитьОбработчикТаймера(() -> ЗагрузитьСписок(), 4мс, Истина)
;

метод ЗаголовокВычисляемый(): Строка
    выбор Периодический
    когда Неопределено
        возврат "Регистр сведений"
    когда Истина
        возврат "Периодический регистр сведений"
    когда Ложь
        возврат "Непериодический регистр сведений"
    ;
;

@НаКлиенте
метод ЗагрузитьСписок()
    если ИсточникМетаданных.Вид == ВидыМетаданных.РегистрСведений
        этот.Периодический = ЯвляетсяПериодическим(ИсточникМетаданных)
        
        знч Элементы = Периодический
            ? ЭлементыПериодическогоРегистра(ИсточникМетаданных)
            : ЭлементыНепериодическогоРегистра(ИсточникМетаданных)
        этот.МассивЭлементов.Данные = Элементы

        если Элементы.Пусто()
            знч КПТ = новый КомпонентПустаяТаблица(Сообщение = "В регистре сведений пусто")
            Компоненты.ГруппаОсновная.Содержимое.Добавить(КПТ)
        ;
    иначе
        Сообщить("Форма предназначена для отображения регистра сведений.\нВид полученного объекта: %{ИсточникМетаданных
                .Вид}")
    ;
;

@НаСервере @ДоступноСКлиента
статический метод ЭлементыНепериодическогоРегистра(Метаданные: ОбъектМетаданных): Массив<СвойстваРегистраСведений>
    знч ТекстЗапроса =
    "ВЫБРАТЬ ПЕРВЫЕ 20
            МетаДанные.КлючЗаписи.ЗаменитьNull(Неопределено) КАК КлючЗаписи,
            МетаДанные.КлючОсновногоФильтра.ЗаменитьNull(Неопределено) КАК КлючОсновногоФильтра
        ИЗ
            %{Метаданные.ТипОбъекта} как МетаДанные
        "
    исп РезультатЗапроса = новый ПроизвольныйЗапрос(ТекстЗапроса).Выполнить()

    возврат РезультатЗапроса.Преобразовать(СРПЗ -> новый СвойстваРегистраСведений(
            КлючЗаписи = СРПЗ.КлючЗаписи,
            КлючОсновногоФильтра = СРПЗ.КлючОсновногоФильтра,
            Период = Неопределено,
            СледующийПериод = Неопределено))
;

@НаСервере @ДоступноСКлиента
статический метод ЭлементыПериодическогоРегистра(Метаданные: ОбъектМетаданных): Массив<СвойстваРегистраСведений>
    знч ТекстЗапроса =
       "ВЫБРАТЬ ПЕРВЫЕ 20
            МетаДанные.КлючЗаписи.ЗаменитьNull(Неопределено) КАК КлючЗаписи,
            МетаДанные.КлючОсновногоФильтра.ЗаменитьNull(Неопределено) КАК КлючОсновногоФильтра,
            МетаДанные.Период.ЗаменитьNull(Неопределено)КАК Период,
            МетаДанные.СледующийПериод.ЗаменитьNull(Неопределено) КАК СледующийПериод
        ИЗ
            %{Метаданные.ТипОбъекта} как МетаДанные
        УПОРЯДОЧИТЬ ПО
            МетаДанные.КлючЗаписи УБЫВ                
        "
    исп РезультатЗапроса = новый ПроизвольныйЗапрос(ТекстЗапроса).Выполнить()

    возврат РезультатЗапроса.Преобразовать(СРПЗ -> новый СвойстваРегистраСведений(
            КлючЗаписи = СРПЗ.КлючЗаписи,
            КлючОсновногоФильтра = СРПЗ.КлючОсновногоФильтра,
            Период = СРПЗ.Период,
            СледующийПериод = СРПЗ.СледующийПериод))
;

@НаСервере @ДоступноСКлиента
статический метод ЯвляетсяПериодическим(Метаданные: ОбъектМетаданных): Булево
    попытка 
        знч ТекстЗапроса =
           "ВЫБРАТЬ ПЕРВЫЕ 1
                МетаДанные.Период.ЗаменитьNull(Неопределено)КАК Период,
                МетаДанные.СледующийПериод.ЗаменитьNull(Неопределено) КАК СледующийПериод
            ИЗ
                %{Метаданные.ТипОбъекта} как МетаДанные
            УПОРЯДОЧИТЬ ПО
                МетаДанные.КлючЗаписи УБЫВ                    
            "
        исп новый ПроизвольныйЗапрос(ТекстЗапроса).Выполнить()
        возврат Истина
    поймать _: Исключение
        возврат Ложь
    ;
;