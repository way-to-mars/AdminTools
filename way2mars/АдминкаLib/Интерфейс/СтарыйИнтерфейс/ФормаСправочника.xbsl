@Обработчик
метод ПослеСоздания()
    ПодключитьОбработчикТаймера(() -> ЗагрузитьСписок(), 4мс, Истина)
;

@НаКлиенте
метод ЗагрузитьСписок()
    если ИсточникМетаданных.Вид == ВидыМетаданных.Справочник
        знч Элементы = ЭлементыСправочника(ИсточникМетаданных)
        этот.МассивЭлементов.Данные = Элементы
        
        если Элементы.Пусто()
            знч КПТ = новый КомпонентПустаяТаблица(Сообщение = "Справочник пуст")
            Компоненты.ГруппаОсновная.Содержимое.Добавить(КПТ)
        ;
    иначе
        Сообщить("Форма предназначена для отображения справочников.\нВид полученного объекта: %{ИсточникМетаданных.Вид}")
    ;
;

@НаСервере @ДоступноСКлиента
статический метод ЭлементыСправочника(Метаданные: ОбъектМетаданных): Массив<СвойстваСправочника>
    знч ТекстЗапроса =
       "ВЫБРАТЬ ПЕРВЫЕ 20
            МетаДанные.Ссылка.ЗаменитьNull(Неопределено) КАК Ссылка,
            МетаДанные.МеткаВерсии.ЗаменитьNull(Неопределено) КАК МеткаВерсии,
            МетаДанные.МоментПометкиУдаления.ЗаменитьNull(Неопределено) КАК МоментПометкиУдаления,
            МетаДанные.ПометкаУдаления.ЗаменитьNull(Неопределено) КАК ПометкаУдаления
        ИЗ
            %{Метаданные.ТипОбъекта} как МетаДанные
        УПОРЯДОЧИТЬ ПО
            МетаДанные.Ссылка УБЫВ                
        "
    исп РезультатЗапроса = новый ПроизвольныйЗапрос(ТекстЗапроса).Выполнить()

    возврат РезультатЗапроса.Преобразовать(СРПЗ -> новый СвойстваСправочника(
            Ссылка = СРПЗ.Ссылка,
            МеткаВерсии = СРПЗ.МеткаВерсии,
            ПометкаУдаления =  СРПЗ.ПометкаУдаления,
            МоментПометкиУдаления = СРПЗ.МоментПометкиУдаления))
;