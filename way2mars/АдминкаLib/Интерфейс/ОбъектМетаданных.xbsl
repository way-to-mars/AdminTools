@НаСервере
@Глобально
статический метод Создать(Источник: Документ|Справочник|ХранилищеНастроек|РегистрСведений|РегистрНакопления,
        ПосчитатьКоличество: Булево): ОбъектМетаданных

    знч ТипИсточника = Источник.ПолучитьТип()
    знч ПредставлениеТипаОбъекта = ТипИсточника.ВСтроку()
    знч ПредставлениеОбъекта = ТипИсточника.Представление()
    пер Количество: Число? = Неопределено
    пер ВидМетаданных: ВидыМетаданных? = Неопределено

    выбор
    когда ТипИсточника.МожетБытьПрисвоенВ(Тип<Документ>)
        знч Парам = новый ПараметрыПолученияЗначенийВыбора()
        ВидМетаданных = ВидыМетаданных.Документ
        если ПосчитатьКоличество
            Количество = (Источник как Документ).ПолучитьСтандартныеЗначенияВыбора(Парам).Размер()
        ;
    когда ТипИсточника.МожетБытьПрисвоенВ(Тип<Справочник>)
        знч Парам = новый ПараметрыПолученияЗначенийВыбора()
        ВидМетаданных = ВидыМетаданных.Справочник
        если ПосчитатьКоличество
            Количество = (Источник как Справочник).ПолучитьСтандартныеЗначенияВыбора(Парам).Размер()
        ;
    когда ТипИсточника.МожетБытьПрисвоенВ(Тип<ХранилищеНастроек>)
        знч Парам = новый ПараметрыПолученияЗначенийВыбора()
        ВидМетаданных = ТипИсточника == Тип<СтандартноеХранилищеНастроек>   // "Std::SettingsStorages::StandardSettingsStorage"
            ? ВидыМетаданных.StdХранилищеНастроек 
            : ВидыМетаданных.ХранилищеНастроек
        если ПосчитатьКоличество
            Количество = (Источник как ХранилищеНастроек).ПолучитьСтандартныеЗначенияВыбора(Парам).Размер()
        ;
    когда ТипИсточника.МожетБытьПрисвоенВ(Тип<РегистрСведений>)
        ВидМетаданных = ВидыМетаданных.РегистрСведений
        если ПосчитатьКоличество
            Количество = КоличествоЭлементов(ПредставлениеТипаОбъекта)
        ;
    когда ТипИсточника.МожетБытьПрисвоенВ(Тип<РегистрНакопления>)
        ВидМетаданных = ВидыМетаданных.РегистрНакопления
        если ПосчитатьКоличество
            Количество = КоличествоЭлементов(ПредставлениеТипаОбъекта)
        ;
    ;
    

    возврат новый ОбъектМетаданных(
            Вид = ВидМетаданных,
            Название = ПредставлениеОбъекта,
            КоличествоЭлементов = Количество,
            ПредставлениеТипаОбъекта = ПредставлениеТипаОбъекта)
;

@НаСервере
@Глобально
статический метод СоздатьПользователей(ПосчитатьКоличество = Истина): ОбъектМетаданных

    знч ТипИсточника = Пользователи.ПолучитьТип()
    знч ПредставлениеТипаОбъекта = ТипИсточника.ВСтроку()
    знч ПредставлениеОбъекта = "Пользователи"
    знч ВидМетаданных = ВидыМетаданных.StdПользователи
    знч Количество = ПосчитатьКоличество ? КоличествоЭлементов("Std::Users::Users") : Неопределено

    возврат новый ОбъектМетаданных(
            Вид = ВидМетаданных,
            Название = ПредставлениеОбъекта,
            КоличествоЭлементов = Количество,
            ПредставлениеТипаОбъекта = ПредставлениеТипаОбъекта)
;

@НаКлиенте @НаСервере @ВПодсистеме
метод ПосчитатьПомеченныеНаУдаление(): Число?
    выбор Вид
    когда Документ, Справочник
        ПомеченныхНаУдаление = КоличествоПомеченныхНаУдаление(этот.ПредставлениеТипаОбъекта)
        возврат ПомеченныхНаУдаление
    ;
    возврат Неопределено
;

@НаКлиенте @НаСервере @ВПодсистеме
метод НайтиПоследнийЭлемент(): Сущность.Ключ?
    выбор Вид
    когда Документ, Справочник, StdХранилищеНастроек, StdПользователи
        ПоследнийСсылка = ПоследнийОбъектныйЭлемент(этот.ПредставлениеТипаОбъекта)
    когда РегистрСведений, РегистрНакопления
        ПоследнийСсылка = ПоследнийНеобъектныйЭлемент(этот.ПредставлениеТипаОбъекта)
    ;
    возврат ПоследнийСсылка
;

// Возвращает Истина, если количество элементов у объекта метаданных изменилось
@ВПодсистеме @НаСервере @НаКлиенте
метод ПосчитатьКоличествоЭлементов(): Булево
    знч Количество = КоличествоЭлементов(этот.ПредставлениеТипаОбъекта)
    
    если Количество != Неопределено и Количество != этот.КоличествоЭлементов
        этот.КоличествоЭлементов = Количество
        возврат Истина
    ;
    возврат Ложь
;

/**
 * Возвращает:
 *    Количество записей в таблице с объектными данными;
 */
@НаСервере @ДоступноСКлиента
статический метод КоличествоЭлементов(ТипМетаданных: Строка): Число?
    попытка
        исп КонтекстДоступа.Привилегированный()
        знч ТекстЗапроса =
           "ВЫБРАТЬ
                КОЛИЧЕСТВО(*) как Количество
            ИЗ
                %ТипМетаданных
            "
        исп РезультатЗапроса = новый ПроизвольныйЗапрос(ТекстЗапроса).Выполнить()
        возврат РезультатЗапроса.Единственный().Количество
    поймать _: Исключение
        возврат Неопределено
    ;
;

@НаСервере @ДоступноСКлиента
статический метод КоличествоПомеченныхНаУдаление(ТипМетаданных: Строка): Число?
    попытка
        исп КонтекстДоступа.Привилегированный()
        знч ТекстЗапроса =
           "ВЫБРАТЬ
                КОЛИЧЕСТВО(ВЫБОР КОГДА МетаДанные.ПометкаУдаления ТОГДА 1 КОНЕЦ) КАК НаУдаление
            ИЗ
                %ТипМетаданных как МетаДанные
            "
        исп РезультатЗапроса = новый ПроизвольныйЗапрос(ТекстЗапроса).Выполнить()
        возврат РезультатЗапроса.Единственный().НаУдаление
    поймать _: Исключение
        возврат Неопределено
    ;
;

@НаСервере @ДоступноСКлиента
статический метод ПоследнийНеобъектныйЭлемент(ТипМетаданных: Строка): Сущность.Ключ?
    попытка
        исп КонтекстДоступа.Привилегированный()
        знч ТекстЗапроса =
           "ВЫБРАТЬ ПЕРВЫЕ 1
                МетаДанные.КлючЗаписи КАК КлючЗаписи
            ИЗ
                %ТипМетаданных как МетаДанные
            УПОРЯДОЧИТЬ ПО
                МетаДанные.КлючЗаписи УБЫВ                
            "
        исп РезультатЗапроса = новый ПроизвольныйЗапрос(ТекстЗапроса).Выполнить()
        возврат РезультатЗапроса.Единственный().КлючЗаписи
    поймать _: Исключение
        возврат Неопределено
    ;
;

@НаСервере @ДоступноСКлиента
статический метод ПоследнийОбъектныйЭлемент(ТипМетаданных: Строка): Сущность.Ключ?
    попытка
        знч ТекстЗапроса = 
           "ВЫБРАТЬ ПЕРВЫЕ 1
                МетаДанные.Ссылка КАК Ссылка
            ИЗ
                %ТипМетаданных как МетаДанные
            УПОРЯДОЧИТЬ ПО
                МетаДанные.Ссылка УБЫВ
            "
        исп КонтекстДоступа.Привилегированный()
        исп РезультатЗапроса = новый ПроизвольныйЗапрос(ТекстЗапроса).Выполнить()
        возврат РезультатЗапроса.Единственный().Ссылка
    поймать _: Исключение
        возврат Неопределено
    ;
;

@Глобально
@НаСервере @ДоступноСКлиента
статический метод ВсеМетаданные(ПосчитатьКоличество = Истина): Массив<ОбъектМетаданных>
    знч Пол = [ОбъектМетаданных.СоздатьПользователей(ПосчитатьКоличество)]
    знч Док = Документы.Преобразовать(Док -> ОбъектМетаданных.Создать(Док, ПосчитатьКоличество))
    знч Спр = Справочники.Преобразовать(Спр -> ОбъектМетаданных.Создать(Спр, ПосчитатьКоличество))
    знч ХН = ХранилищаНастроек.Преобразовать(ХН -> ОбъектМетаданных.Создать(ХН, ПосчитатьКоличество))
    знч РС = РегистрыСведений.Преобразовать(РС -> ОбъектМетаданных.Создать(РС, ПосчитатьКоличество))
    знч РН = РегистрыНакопления.Преобразовать(РН -> ОбъектМетаданных.Создать(РН, ПосчитатьКоличество))

    возврат Пол.Объединить(Док).Объединить(Спр).Объединить(РС).Объединить(РН).Объединить(ХН)
;
