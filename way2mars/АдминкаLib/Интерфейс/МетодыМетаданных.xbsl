/**
  Описание:
    Возвращает список всех объектов метаданных приложения:
    Пользователи, Хранилища Настроек, Справочники, Документы, Регистры, Планы Обмена.
    Список отсортирован по краткому наименованию объектов метаданных в лексикографическом порядке.
 */ 
@ВПодсистеме @НаСервере @ДоступноСКлиента
метод СортированныйСписокМетаданных(): Массив<ОбъектМетаданных>
    
    знч Пол = [ОбработатьСправочникПользователей()]
    знч ХН = ХранилищаНастроек.Преобразовать(&ОбработатьХранилищеНастроек)
    знч Спр = Справочники.Преобразовать(&ОбработатьСправочник)
    знч Док = Документы.Преобразовать(&ОбработатьДокумент)
    знч РС = РегистрыСведений.Преобразовать(&ОбработатьРегистрСведений)
    знч РН = РегистрыНакопления.Преобразовать(&ОбработатьРегистрНакопления)
    знч ПО = ПланыОбмена.Преобразовать(&ОбработатьПланОбмена)
    
    возврат Пол
        .Объединить(Док)
        .Объединить(Спр)
        .Объединить(РС)
        .Объединить(РН)
        .Объединить(ХН)
        .Объединить(ПО)
        .СортироватьПо(ОМ -> ОМ.Название)
;

@НаСервере
метод СсылочныеРеквизитыОбъектнойСущности(ТипСущности: Тип): Массив<СсылочныйРеквизит>
    попытка
        знч ООС = ОтражениеСущности.ПоТипу(ТипСущности) как ОтражениеОбъектнойСущности
        знч СсылочныеРеквизиты = <СсылочныйРеквизит>[]
        
        для Реквизит из ООС.Реквизиты
            если Реквизит.Имя.Английский == "Reference" и Реквизит.Имя.Русский == "Ссылка"
                продолжить
            ;
            
            знч Разновидность = ОпределитьВидыСсылок(Реквизит.Типы)
            если Разновидность != Неопределено
                СсылочныеРеквизиты.Добавить(новый СсылочныйРеквизит(
                    Реквизит.Имя.Русский,
                    Разновидность,
                    ВидыРеквизитов.Реквизит                    
                ))
            ;
        ;
        
        возврат СсылочныеРеквизиты
    поймать Искл: Исключение
        ЛогиИнтерфейса.Исключение("СсылочныеРеквизитыОбъектнойСущности", Искл)
        возврат []
    ;
;

@НаСервере
метод СсылочныеРеквизитыСущностиРегистра(ТипСущности: Тип): Массив<СсылочныйРеквизит>
    попытка
        знч ООС = ОтражениеСущности.ПоТипу(ТипСущности) как ОтражениеСущностиРегистра
        знч СсылочныеРеквизиты = <СсылочныйРеквизит>[]
        
        для Реквизит из ООС.Измерения
            знч Разновидность = ОпределитьВидыСсылок(Реквизит.Типы)
            если Разновидность != Неопределено
                СсылочныеРеквизиты.Добавить(новый СсылочныйРеквизит(
                    Реквизит.Имя.Русский,
                    Разновидность,
                    ВидыРеквизитов.Измерение
                ))
            ;
        ;
        
        для Реквизит из ООС.Ресурсы
            знч Разновидность = ОпределитьВидыСсылок(Реквизит.Типы)
            если Разновидность != Неопределено
                СсылочныеРеквизиты.Добавить(новый СсылочныйРеквизит(
                    Реквизит.Имя.Русский,
                    Разновидность,
                    ВидыРеквизитов.Ресурс
                ))
            ;
        ;
        
        для Реквизит из ООС.Реквизиты
            знч Разновидность = ОпределитьВидыСсылок(Реквизит.Типы)
            если Разновидность != Неопределено
                СсылочныеРеквизиты.Добавить(новый СсылочныйРеквизит(
                    Реквизит.Имя.Русский,
                    Разновидность,
                    ВидыРеквизитов.Реквизит
                ))
            ;
        ;

        возврат СсылочныеРеквизиты
    поймать Искл: Исключение
        ЛогиИнтерфейса.Исключение("СсылочныеРеквизитыСущностиРегистра", Искл)
        возврат []
    ;
;

@НаСервере
метод ОпределитьВидыСсылок(ТипОбъекта: Тип): ВидыСсылок?
    // Игнорируем Сущность.Ключ, т.к. у него нет параметров
    // если ТипОбъекта == Тип<Сущность.Ключ> 
    //     возврат ВидыСсылок.Смешанный
    // ;
    
    пер МаскаРазновидности = 0
    если ТипОбъекта.МожетБытьПрисвоенВ({Тип<РегистрНакопления.КлючЗаписи>, Тип<РегистрСведений.КлючЗаписи>})
        МаскаРазновидности += 1
    ;
    
    если ТипОбъекта.МожетБытьПрисвоенВ(Тип<Сущность.Ссылка>)
        МаскаРазновидности += 2
    ;
    
    выбор МаскаРазновидности
    когда 1
        возврат ВидыСсылок.СущностьРегистра
    когда 2
        возврат ВидыСсылок.ОбъектнаяСущность
    когда 3
        возврат ВидыСсылок.Смешанный
    иначе
        возврат Неопределено
    ;
;

@НаСервере
метод ОпределитьВидыСсылок(ТипыОбъектов: ЧитаемаяКоллекция<Тип>): ВидыСсылок?
    знч Виды = ТипыОбъектов.Преобразовать(&ОпределитьВидыСсылок, Истина)
    
    пер МаскаРазновидности = 0
    если Виды.Содержит(ВидыСсылок.СущностьРегистра)
        МаскаРазновидности += 1
    ;
    
    если Виды.Содержит(ВидыСсылок.ОбъектнаяСущность)
        МаскаРазновидности += 2
    ;
    
    если Виды.Содержит(ВидыСсылок.Смешанный)
        МаскаРазновидности = 3
    ;
    
    выбор МаскаРазновидности
    когда 1
        возврат ВидыСсылок.СущностьРегистра
    когда 2
        возврат ВидыСсылок.ОбъектнаяСущность
    когда 3
        возврат ВидыСсылок.Смешанный
    иначе
        возврат Неопределено
    ;
;

@НаСервере
метод ОбработатьСправочникПользователей(): ОбъектМетаданных
    знч ТипИсточника = Пользователи.ПолучитьТип()
    знч ПредставлениеТипаОбъекта = ТипИсточника.ВСтроку()
    знч ПредставлениеОбъекта = "Пользователи"
    знч ВидМетаданных = ВидыМетаданных.StdПользователи
    
    попытка
        исп КонтекстДоступа.Привилегированный()
        исп РезультатЗапроса = Запрос{
            ВЫБРАТЬ
                МАКСИМУМ(Пользователи.Ссылка).ЗаменитьNull(Неопределено) КАК СсылкаПоследнего,
                КОЛИЧЕСТВО(*) как ВсегоЗаписей
            ИЗ
                Std::Users::Users КАК Пользователи
        }.Выполнить()
        знч СтрокаРезультата = РезультатЗапроса.ЕдинственныйИлиНеопределено()
        знч Количество = СтрокаРезультата?.ВсегоЗаписей
        знч Последний = СтрокаРезультата?.СсылкаПоследнего
        
        возврат новый ОбъектМетаданных(
            Вид = ВидМетаданных,
            Название = ПредставлениеОбъекта,
            КоличествоЭлементов = Количество,
            ПредставлениеТипаОбъекта = ПредставлениеТипаОбъекта,
            ТипОбъекта = Неопределено,
            ПоследнийСсылка = Последний,
            СсылочныеРеквизиты = [])
            
    поймать Искл: Исключение
        ЛогиИнтерфейса.Исключение("ОбработатьСправочникПользователей", Искл)
        возврат новый ОбъектМетаданных(
            Вид = ВидМетаданных,
            Название = ПредставлениеОбъекта,
            КоличествоЭлементов = Неопределено,
            ПредставлениеТипаОбъекта = ПредставлениеТипаОбъекта,
            ТипОбъекта = Неопределено,
            ПоследнийСсылка = Неопределено,
            СсылочныеРеквизиты = [])
    ;  
;

@НаСервере
метод ОбработатьСтандартноеХранилищеНастроек(): ОбъектМетаданных
    знч ТипИсточника = СтандартноеХранилищеНастроек.ПолучитьТип()
    знч ПредставлениеТипаОбъекта = ТипИсточника.ВСтроку()
    знч ПредставлениеОбъекта = "Стандартное хранилище настроек"
    знч ВидМетаданных = ВидыМетаданных.StdХранилищеНастроек
    знч Количество = СтандартноеХранилищеНастроек
        .ПолучитьСтандартныеЗначенияВыбора(новый ПараметрыПолученияЗначенийВыбора())
        .Размер()

    возврат новый ОбъектМетаданных(
        Вид = ВидМетаданных,
        Название = ПредставлениеОбъекта,
        КоличествоЭлементов = Количество,
        ПредставлениеТипаОбъекта = ПредставлениеТипаОбъекта,
        ТипОбъекта = Тип<СтандартноеХранилищеНастроек>,
        СсылочныеРеквизиты = [])
;

@НаСервере
метод ОбработатьХранилищеНастроек(Источник: ХранилищеНастроек): ОбъектМетаданных
    если Источник == СтандартноеХранилищеНастроек
        возврат ОбработатьСтандартноеХранилищеНастроек()
    ;
    
    знч ТипИсточника = Источник.ПолучитьТип()
    знч ПредставлениеТипаОбъекта = ТипИсточника.ВСтроку()
    знч ПредставлениеОбъекта = ТипИсточника.Представление()
    знч ВидМетаданных = ВидыМетаданных.ХранилищеНастроек
    
    знч Количество = Источник
        .ПолучитьСтандартныеЗначенияВыбора(новый ПараметрыПолученияЗначенийВыбора())
        .Размер()

    возврат новый ОбъектМетаданных(
        Вид = ВидМетаданных,
        Название = ПредставлениеОбъекта,
        КоличествоЭлементов = Количество,
        ПредставлениеТипаОбъекта = ПредставлениеТипаОбъекта,
        ТипОбъекта = ТипИсточника,
        СсылочныеРеквизиты = МетодыМетаданных.СсылочныеРеквизитыОбъектнойСущности(ТипИсточника))
;

@НаСервере
метод ОбработатьСправочник(Источник: Справочник): ОбъектМетаданных
    знч ТипИсточника = Источник.ПолучитьТип()
    знч ПредставлениеТипаОбъекта = ТипИсточника.ВСтроку()
    знч ПредставлениеОбъекта = ТипИсточника.Представление()
    знч ВидМетаданных = ВидыМетаданных.Справочник
    
    знч СсылочныеРеквизиты = МетодыМетаданных.СсылочныеРеквизитыОбъектнойСущности(ТипИсточника)
    
    попытка
        исп КонтекстДоступа.Привилегированный()
        
        знч ВыборПомеченных = (ОтражениеСущности.ПоТипу(ТипИсточника) как ОтражениеОбъектнойСущности)
            .Реквизиты
            .ЕстьСоответствия(ОХС -> ОХС.Имя.Русский == "ПометкаУдаления")
                ? "КОЛИЧЕСТВО(ВЫБОР КОГДА МетаДанные.ПометкаУдаления ТОГДА 1 КОНЕЦ)"
                : "Неопределено"
                
        знч ТекстЗапроса =
            "ВЫБРАТЬ
                МАКСИМУМ(МетаДанные.Ссылка).ЗаменитьNull(Неопределено) КАК СсылкаПоследнего,
                КОЛИЧЕСТВО(*) как ВсегоЗаписей,
                %{ВыборПомеченных} КАК ПомеченоНаУдаление
            ИЗ
                %ПредставлениеТипаОбъекта как МетаДанные
            "
        исп РезультатЗапроса = новый ПроизвольныйЗапрос(ТекстЗапроса).Выполнить()
        знч СтрокаРезультата = РезультатЗапроса.ЕдинственныйИлиНеопределено()
        знч Количество = СтрокаРезультата?.ВсегоЗаписей
        знч Последний = СтрокаРезультата?.СсылкаПоследнего
        знч ПомеченоНаУдаление = СтрокаРезультата?.ПомеченоНаУдаление

        возврат новый ОбъектМетаданных(
            Вид = ВидМетаданных,
            Название = ПредставлениеОбъекта,
            КоличествоЭлементов = Количество,
            ПредставлениеТипаОбъекта = ПредставлениеТипаОбъекта,
            ТипОбъекта = ТипИсточника,
            ПомеченныхНаУдаление = ПомеченоНаУдаление,
            ПоследнийСсылка = Последний,
            СсылочныеРеквизиты = СсылочныеРеквизиты)
    поймать Искл: Исключение
        ЛогиИнтерфейса.Исключение("ОбработатьСправочник", Искл)
        возврат новый ОбъектМетаданных(
            Вид = ВидМетаданных,
            Название = ПредставлениеОбъекта,
            КоличествоЭлементов = Неопределено,
            ПредставлениеТипаОбъекта = ПредставлениеТипаОбъекта,
            ТипОбъекта = ТипИсточника,
            СсылочныеРеквизиты = СсылочныеРеквизиты)
    ;   
;

@НаСервере
метод ОбработатьДокумент(Источник: Документ): ОбъектМетаданных
    знч ТипИсточника = Источник.ПолучитьТип()
    знч ПредставлениеТипаОбъекта = ТипИсточника.ВСтроку()
    знч ПредставлениеОбъекта = ТипИсточника.Представление()
    знч ВидМетаданных = ВидыМетаданных.Документ
    
    знч СсылочныеРеквизиты = МетодыМетаданных.СсылочныеРеквизитыОбъектнойСущности(ТипИсточника)

    попытка
        исп КонтекстДоступа.Привилегированный()
        
        знч ВыборПомеченных = (ОтражениеСущности.ПоТипу(ТипИсточника) как ОтражениеОбъектнойСущности)
            .Реквизиты
            .ЕстьСоответствия(ОХС -> ОХС.Имя.Русский == "ПометкаУдаления")
                ? "КОЛИЧЕСТВО(ВЫБОР КОГДА МетаДанные.ПометкаУдаления ТОГДА 1 КОНЕЦ)"
                : "Неопределено"
        
        знч ТекстЗапроса =
            "ВЫБРАТЬ
                МАКСИМУМ(МетаДанные.Ссылка).ЗаменитьNull(Неопределено) КАК СсылкаПоследнего,
                КОЛИЧЕСТВО(*) как ВсегоЗаписей,
                %{ВыборПомеченных} КАК ПомеченоНаУдаление
            ИЗ
                %ПредставлениеТипаОбъекта как МетаДанные
            "
        исп РезультатЗапроса = новый ПроизвольныйЗапрос(ТекстЗапроса).Выполнить()
        знч СтрокаРезультата = РезультатЗапроса.ЕдинственныйИлиНеопределено()
        знч Количество = СтрокаРезультата?.ВсегоЗаписей
        знч Последний = СтрокаРезультата?.СсылкаПоследнего
        знч ПомеченоНаУдаление = СтрокаРезультата?.ПомеченоНаУдаление

        возврат новый ОбъектМетаданных(
            Вид = ВидМетаданных,
            Название = ПредставлениеОбъекта,
            КоличествоЭлементов = Количество,
            ПредставлениеТипаОбъекта = ПредставлениеТипаОбъекта,
            ТипОбъекта = ТипИсточника,
            ПомеченныхНаУдаление = ПомеченоНаУдаление,
            ПоследнийСсылка = Последний,
            СсылочныеРеквизиты = СсылочныеРеквизиты)
    поймать Искл: Исключение
        ЛогиИнтерфейса.Исключение("ОбработатьДокумент", Искл)
        возврат новый ОбъектМетаданных(
            Вид = ВидМетаданных,
            Название = ПредставлениеОбъекта,
            КоличествоЭлементов = Неопределено,
            ПредставлениеТипаОбъекта = ПредставлениеТипаОбъекта,
            ТипОбъекта = ТипИсточника,
            СсылочныеРеквизиты = СсылочныеРеквизиты)
    ;   
;

@НаСервере
метод ОбработатьПланОбмена(Источник: ПланОбмена): ОбъектМетаданных
    знч ТипИсточника = Источник.ПолучитьТип()
    знч ПредставлениеТипаОбъекта = ТипИсточника.ВСтроку()
    знч ПредставлениеОбъекта = ТипИсточника.Представление()
    знч ВидМетаданных = ВидыМетаданных.ПланОбмена
    
    знч СсылочныеРеквизиты = МетодыМетаданных.СсылочныеРеквизитыОбъектнойСущности(ТипИсточника)

    попытка
        исп КонтекстДоступа.Привилегированный()
        
        знч ВыборПомеченных = (ОтражениеСущности.ПоТипу(ТипИсточника) как ОтражениеОбъектнойСущности)
            .Реквизиты
            .ЕстьСоответствия(ОХС -> ОХС.Имя.Русский == "ПометкаУдаления")
                ? "КОЛИЧЕСТВО(ВЫБОР КОГДА МетаДанные.ПометкаУдаления ТОГДА 1 КОНЕЦ)"
                : "Неопределено"        
        
        знч ТекстЗапроса =
            "ВЫБРАТЬ
                МАКСИМУМ(МетаДанные.Ссылка).ЗаменитьNull(Неопределено) КАК СсылкаПоследнего,
                КОЛИЧЕСТВО(*) как ВсегоЗаписей,
                %{ВыборПомеченных} КАК ПомеченоНаУдаление
            ИЗ
                %ПредставлениеТипаОбъекта как МетаДанные
            "
        исп РезультатЗапроса = новый ПроизвольныйЗапрос(ТекстЗапроса).Выполнить()
        знч СтрокаРезультата = РезультатЗапроса.ЕдинственныйИлиНеопределено()
        знч Количество = СтрокаРезультата?.ВсегоЗаписей
        знч Последний = СтрокаРезультата?.СсылкаПоследнего
        знч ПомеченоНаУдаление = СтрокаРезультата?.ПомеченоНаУдаление

        возврат новый ОбъектМетаданных(
            Вид = ВидМетаданных,
            Название = ПредставлениеОбъекта,
            КоличествоЭлементов = Количество,
            ПредставлениеТипаОбъекта = ПредставлениеТипаОбъекта,
            ТипОбъекта = ТипИсточника,
            ПомеченныхНаУдаление = ПомеченоНаУдаление,
            ПоследнийСсылка = Последний,
            СсылочныеРеквизиты = СсылочныеРеквизиты)
    поймать Искл: Исключение
        ЛогиИнтерфейса.Исключение("ОбработатьПланОбмена", Искл)
        возврат новый ОбъектМетаданных(
            Вид = ВидМетаданных,
            Название = ПредставлениеОбъекта,
            КоличествоЭлементов = Неопределено,
            ПредставлениеТипаОбъекта = ПредставлениеТипаОбъекта,
            ТипОбъекта = ТипИсточника,
            СсылочныеРеквизиты = СсылочныеРеквизиты)
    ;   
;

@НаСервере
метод ОбработатьРегистрСведений(Источник: РегистрСведений): ОбъектМетаданных
    знч ТипИсточника = Источник.ПолучитьТип()
    знч ПредставлениеТипаОбъекта = ТипИсточника.ВСтроку()
    знч ПредставлениеОбъекта = ТипИсточника.Представление()

    знч Отражение = ОтражениеСущности.ПоТипу(ТипИсточника) как ОтражениеСущностиРегистра
    знч ВидМетаданных = Отражение.Измерения.ЕстьСоответствия(ОХС -> ОХС.Имя.Английский == "Period")
        ? ВидыМетаданных.РегистрСведений : ВидыМетаданных.РегистрСведенийПериодический

    знч СсылочныеРеквизиты = МетодыМетаданных.СсылочныеРеквизитыСущностиРегистра(ТипИсточника)

    попытка
        исп КонтекстДоступа.Привилегированный()
        знч ТекстЗапроса =
        "ВЫБРАТЬ
                КОЛИЧЕСТВО(*) как ВсегоЗаписей
            ИЗ
                %ПредставлениеТипаОбъекта как МетаДанные
            "
        исп РезультатЗапроса = новый ПроизвольныйЗапрос(ТекстЗапроса).Выполнить()
        знч СтрокаРезультата = РезультатЗапроса.ЕдинственныйИлиНеопределено()
        знч Количество = СтрокаРезультата?.ВсегоЗаписей

        возврат новый ОбъектМетаданных(
            Вид = ВидМетаданных,
            Название = ПредставлениеОбъекта,
            КоличествоЭлементов = Количество,
            ПредставлениеТипаОбъекта = ПредставлениеТипаОбъекта,
            ТипОбъекта = ТипИсточника,
            СсылочныеРеквизиты = СсылочныеРеквизиты)
    поймать Искл: Исключение
        ЛогиИнтерфейса.Исключение("ОбработатьРегистрСведений", Искл)
        возврат новый ОбъектМетаданных(
                Вид = ВидМетаданных,
                Название = ПредставлениеОбъекта,
                КоличествоЭлементов = Неопределено,
                ПредставлениеТипаОбъекта = ПредставлениеТипаОбъекта,
                ТипОбъекта = ТипИсточника,
                СсылочныеРеквизиты = СсылочныеРеквизиты)
    ;
;

@НаСервере
метод ОбработатьРегистрНакопления(Источник: РегистрНакопления): ОбъектМетаданных
    знч ТипИсточника = Источник.ПолучитьТип()
    знч ПредставлениеТипаОбъекта = ТипИсточника.ВСтроку()
    знч ПредставлениеОбъекта = ТипИсточника.Представление()
    знч ВидМетаданных = ВидыМетаданных.РегистрНакопления

    знч СсылочныеРеквизиты = МетодыМетаданных.СсылочныеРеквизитыСущностиРегистра(ТипИсточника)

    попытка
        исп КонтекстДоступа.Привилегированный()
        знч ТекстЗапроса =
        "ВЫБРАТЬ
                КОЛИЧЕСТВО(*) как ВсегоЗаписей
            ИЗ
                %ПредставлениеТипаОбъекта как МетаДанные
            "
        исп РезультатЗапроса = новый ПроизвольныйЗапрос(ТекстЗапроса).Выполнить()
        знч СтрокаРезультата = РезультатЗапроса.ЕдинственныйИлиНеопределено()
        знч Количество = СтрокаРезультата?.ВсегоЗаписей

        возврат новый ОбъектМетаданных(
            Вид = ВидМетаданных,
            Название = ПредставлениеОбъекта,
            КоличествоЭлементов = Количество,
            ПредставлениеТипаОбъекта = ПредставлениеТипаОбъекта,
            ТипОбъекта = ТипИсточника,
            СсылочныеРеквизиты = СсылочныеРеквизиты)
    поймать Искл: Исключение
        ЛогиИнтерфейса.Исключение("ОбработатьРегистрНакопления", Искл)
        возврат новый ОбъектМетаданных(
                Вид = ВидМетаданных,
                Название = ПредставлениеОбъекта,
                КоличествоЭлементов = Неопределено,
                ПредставлениеТипаОбъекта = ПредставлениеТипаОбъекта,
                ТипОбъекта = ТипИсточника,
                СсылочныеРеквизиты = СсылочныеРеквизиты)
    ;
;

метод СтрокаВыбора(ИмяМетаданных: Строка, Реквизит: СсылочныйРеквизит): Строка
    выбор Реквизит.РазновидностьСсылки
    когда ВидыСсылок.ОбъектнаяСущность
        возврат
           "  ВЫБОР
                КОГДА %{ИмяМетаданных}.%{Реквизит.Имя}.Ид ЕСТЬ NULL
                ТОГДА \"битая ссылка\"
              ИНАЧЕ
                %{ИмяМетаданных}.%{Реквизит.Имя}
              КОНЕЦ КАК %{Реквизит.Имя}"
              
    когда ВидыСсылок.СущностьРегистра
        возврат "  %{ИмяМетаданных}.%{Реквизит.Имя}.КлючЗаписи.ЗаменитьNull(\"битая ссылка\") как %{Реквизит.Имя}"
        
    когда ВидыСсылок.Смешанный
        возврат
           "  ВЫБОР
                КОГДА %{ИмяМетаданных}.%{Реквизит.Имя}.Ид ЕСТЬ NULL
                  ТОГДА %{ИмяМетаданных}.%{Реквизит.Имя}.КлючЗаписи.ЗаменитьNull(\"битая ссылка\")
                ИНАЧЕ
                  %{ИмяМетаданных}.%{Реквизит.Имя}.Ид.ЗаменитьNull(\"битая ссылка\")
              КОНЕЦ КАК %{Реквизит.Имя}"
    ;    
;

метод СтрокаУсловия(ИмяМетаданных: Строка, Реквизит: СсылочныйРеквизит): Строка
    выбор Реквизит.РазновидностьСсылки
    когда ВидыСсылок.ОбъектнаяСущность
        возврат "  %{ИмяМетаданных}.%{Реквизит.Имя}.Ид ЕСТЬ NULL"
    когда ВидыСсылок.СущностьРегистра
        возврат "  %{ИмяМетаданных}.%{Реквизит.Имя}.КлючОсновногоФильтра ЕСТЬ NULL"
    когда ВидыСсылок.Смешанный
        возврат
           "  (%{ИмяМетаданных}.%{Реквизит.Имя}.Ид ЕСТЬ NULL И
              %{ИмяМетаданных}.%{Реквизит.Имя}.КлючОсновногоФильтра ЕСТЬ NULL)"
    ;    
;

@ВПодсистеме 
метод ЗапросБитыхСсылок(Сущность: ОбъектМетаданных): Строка
    
    знч КлючПоВидуМетаданных = метод(Вид: ВидыМетаданных?) ->
        выбор Вид
        когда Документ, Справочник, ХранилищеНастроек, ПланОбмена, StdПользователи, StdХранилищеНастроек
            возврат "Ссылка"
        когда РегистрНакопления, РегистрСведений, РегистрСведенийПериодический
            возврат "КлючЗаписи"
        иначе
            выбросить новый ИсключениеНедопустимоеСостояние("Недопустимое значение вида метаданных: $Вид")
        ;
    ;
    
    знч Ключ = КлючПоВидуМетаданных(Сущность.Вид)
    
    знч СтрокиВыбора = Сущность.СсылочныеРеквизиты.Преобразовать(СР -> СтрокаВыбора("ОбъектМетаданных", СР))
    знч СтрокиУсловия = Сущность.СсылочныеРеквизиты.Преобразовать(СР -> СтрокаУсловия("ОбъектМетаданных", СР))
    
    знч ТекстЗапроса = 
       "ВЫБРАТЬ
          ОбъектМетаданных.%{Ключ},
        %{Строки.Соединить(СтрокиВыбора, ",\н")}
        ИЗ
          %{Сущность.ПредставлениеТипаОбъекта} КАК ОбъектМетаданных
        ГДЕ
        %{Строки.Соединить(СтрокиУсловия, " ИЛИ\н")}"
    
    возврат ТекстЗапроса
;