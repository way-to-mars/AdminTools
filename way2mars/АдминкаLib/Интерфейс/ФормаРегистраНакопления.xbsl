@Обработчик
метод ПослеСоздания()
    ПодключитьОбработчикТаймера(() -> ЗагрузитьСписок(), 4мс, Истина)
;

@НаКлиенте
метод ЗагрузитьСписок()
    если ИсточникМетаданных.Вид == ВидыМетаданных.РегистрНакопления

        знч Элементы = ЭлементыРегистраНакоплений(этот.ИсточникМетаданных)
        этот.МассивЭлементов.Данные = Элементы

        если Элементы.Пусто()
            знч КПТ = новый КомпонентПустаяТаблица(Сообщение = "В регистре накопления пусто")
            Компоненты.ГруппаОсновная.Содержимое.Добавить(КПТ)
        ;
    иначе
        Сообщить("Форма предназначена для отображения регистра накопления.\нВид полученного объекта: %{ИсточникМетаданных
                .Вид}")
    ;
;

@НаСервере @ДоступноСКлиента
статический метод ЭлементыРегистраНакоплений(Метаданные: ОбъектМетаданных): Массив<СвойстваРегистраНакопления>
    знч ТекстЗапроса =
    "ВЫБРАТЬ ПЕРВЫЕ 20
            МетаДанные.Регистратор.ЗаменитьNull(Неопределено) КАК Регистратор,
            МетаДанные.Индекс.ЗаменитьNull(Неопределено) КАК Индекс,
            МетаДанные.КлючОсновногоФильтра.ЗаменитьNull(Неопределено)КАК КлючОсновногоФильтра,
            МетаДанные.Период.ЗаменитьNull(Неопределено) КАК Период,
            МетаДанные.Активность.ЗаменитьNull(Неопределено) КАК Активность,
            МетаДанные.ВидЗаписи.ЗаменитьNull(Неопределено) КАК ВидЗаписи,
            МетаДанные.НомерСтроки.ЗаменитьNull(Неопределено) КАК НомерСтроки,
            МетаДанные.КлючЗаписи.ЗаменитьNull(Неопределено) КАК КлючЗаписи
        ИЗ
            %{Метаданные.ТипОбъекта} как МетаДанные
        УПОРЯДОЧИТЬ ПО
            МетаДанные.КлючЗаписи УБЫВ                
        "
    исп РезультатЗапроса = новый ПроизвольныйЗапрос(ТекстЗапроса).Выполнить()

    возврат РезультатЗапроса.Преобразовать(СРПЗ -> новый СвойстваРегистраНакопления(
            КлючЗаписи = СРПЗ.КлючЗаписи,
            Активность = СРПЗ.Активность,
            Индекс = СРПЗ.Индекс,
            Регистратор = СРПЗ.Регистратор,
            ВидЗаписи = СРПЗ.ВидЗаписи,
            Период = СРПЗ.Период,
            НомерСтроки = СРПЗ.НомерСтроки,
            КлючОсновногоФильтра = СРПЗ.КлючОсновногоФильтра))
;

