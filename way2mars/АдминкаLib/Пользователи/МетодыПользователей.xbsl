@НаСервере @ДоступноСКлиента @ВПодсистеме
статический метод ЗагрузитьПользователя(ПользовательСсылка: Пользователи.Ссылка): СвойстваПользователя
    возврат СвойстваПользователя.ЗагрузитьОбъект(ПользовательСсылка)
;

@НаСервере @ДоступноСКлиента @ВПодсистеме
статический метод СделатьАдминистратором(Пользователь: Пользователи.Ссылка, Администратор: Булево): Строка?
    попытка 
        Пользователи.СделатьАдминистратором(Пользователь, Администратор)
        возврат Неопределено
    поймать Искл: Исключение
        возврат Искл.Описание
    ;
;

/**
 * Сомнительно, что сработает
 */
@НаСервере @ДоступноСКлиента @ВПодсистеме
статический метод УстановитьУспешныйВход(Пользователь: Пользователи.Ссылка, БылУспешныйВход: Булево): Строка?
    попытка 
        знч ПО = Пользователь.ЗагрузитьОбъект()
        ПО.БылУспешныйВход = БылУспешныйВход
        // (ПО как неизвестно).Записать()
        возврат Неопределено
    поймать Искл: Исключение
        возврат Искл.Описание
    ;
;

@НаСервере @ДоступноСКлиента @ВПодсистеме
статический метод ЗапретитьВход(Пользователь: Пользователи.Ссылка, ЗапретитьВход: Булево): Строка?
    попытка
        Пользователи.ЗапретитьВход(Пользователь, ЗапретитьВход)
        возврат Неопределено
    поймать Искл: Исключение
        возврат Искл.Описание
    ;
;

@НаСервере @ДоступноСКлиента @ВПодсистеме
статический метод РазрешитьДоступПоТокену(Пользователь: Пользователи.Ссылка, РазрешитьДоступ: Булево): Строка?
    попытка
        Пользователи.РазрешитьДоступПоТокену(Пользователь, РазрешитьДоступ)
        возврат Неопределено
    поймать Искл: Исключение
        возврат Искл.Описание
    ;
;

@НаСервере @ДоступноСКлиента @ВПодсистеме
статический метод УстановитьЛокальОтображения(Пользователь: Пользователи.Ссылка, Локаль: Локаль?): Строка?
    попытка 
        Пользователи.УстановитьЛокальОтображения(Пользователь, Локаль)
        возврат Неопределено
    поймать Искл: Исключение
        возврат Искл.Описание
    ;        
;

@НаСервере @ДоступноСКлиента @ВПодсистеме
статический метод УстановитьЛокальФорматирования(Пользователь: Пользователи.Ссылка, Локаль: Локаль?): Строка?
    попытка 
        Пользователи.УстановитьЛокальФорматирования(Пользователь, Локаль)
        возврат Неопределено
    поймать Искл: Исключение
        возврат Искл.Описание
    ;
;

@НаСервере @ДоступноСКлиента @ВПодсистеме
статический метод УстановитьУтверждения(Пользователь: Пользователи.Ссылка, Утверждения: ЧитаемоеСоответствие<Строка, Строка>): Строка?
    попытка
        Пользователи.УстановитьУтверждения(Пользователь, Утверждения)
        возврат Неопределено
    поймать Искл: Исключение
        возврат Искл.Описание
    ;    
;


/**
  Описание:
    Отключает пользователя приложения Пользователь. Удаляет запись пользователя из БД.
 */
@НаСервере @ДоступноСКлиента @ВПодсистеме
статический метод Отключить(Пользователь: Пользователи.Ссылка): Строка?
    попытка
        Пользователи.Отключить(Пользователь)
        возврат Неопределено
    поймать Искл: Исключение
        возврат Искл.Описание
    ;  
;

/**
  Описание:
    https://1cmycloud.com/console/help/element/8.1/docs/stdlib/element/Std/Users/Users_ru/
 */
@НаСервере @ДоступноСКлиента @ВПодсистеме
метод СоздатьПользователя(Представление: Строка, Телефон: Строка, ЭлПочта: Строка): Пользователи.Ссылка
    // У пользователя может быть несколько учетных записей, созданных в разных сервисах аутентификации.
    // Здесь создание локального сервиса учетных записей выполняется с целью упрощения примера.
    знч УчетныеЗаписи = {СоздатьУчетнуюЗапись(Представление, СоздатьСервисУчетныхЗаписейЛокальный())}
    
    // Создание описания пользователя в списке пользователей по умолчанию
    пер ОписаниеПользователяСервиса = новый ОписаниеПользователяСервиса(СпискиПользователей.ПолучитьСписокПоУмолчанию().Ид, Представление)
    ОписаниеПользователяСервиса = ОписаниеПользователяСервиса
                            .СЛогином(Представление)
                            .СТелефоном(Телефон)
                            .СЭлектроннойПочтой(ЭлПочта)
                            .СУчетнымиЗаписями(УчетныеЗаписи)
        
    // Требуются права администратора или привилегированный режим, иначе будет исключение 
    ПользователиСервиса.Создать(ОписаниеПользователяСервиса)
        
    // Подключение пользователя к приложению
    // Требуются права администратора или привилегированный режим, иначе будет исключение
    знч ПользовательСсылка = Пользователи.Подключить(ОписаниеПользователяСервиса.Ид).Ссылка
    
    // Выдать пользователю права администратора
    Пользователи.СделатьАдминистратором(ПользовательСсылка, Истина)
        
    возврат ПользовательСсылка
;

/**
  Описание:
    https://1cmycloud.com/console/help/element/8.1/docs/stdlib/element/Std/Users/Users_ru/
 */
@НаСервере @ДоступноСКлиента @ВПодсистеме
метод СоздатьУчетнуюЗапись(Ид: Строка, СервисУчетныхЗаписей: СервисУчетныхЗаписей): УчетнаяЗапись
    возврат новый УчетнаяЗапись(Ид, СервисУчетныхЗаписей)
;

/**
  Описание:
    https://1cmycloud.com/console/help/element/8.1/docs/stdlib/element/Std/Users/Users_ru/
 */
@НаСервере @ДоступноСКлиента @ВПодсистеме
метод СоздатьСервисУчетныхЗаписейЛокальный(): СервисУчетныхЗаписей
    знч Сервис = новый СервисУчетныхЗаписей(ВидСервисаУчетныхЗаписей.Локальный, "LOCAL")
    
    знч НастройкиСервиса = новый НастройкиСервисаУчетныхЗаписей(Сервис)
    НастройкиСервиса.Включено = Истина
    НастройкиСервиса.СоздаватьПользователяПриВходе = Ложь
    
    возврат Сервис
;

/**
  Описание:
    https://1cmycloud.com/console/help/element/8.1/docs/stdlib/element/Std/Users/TwoFactorAuthentication_ru/
 */
@НаСервере @ДоступноСКлиента @ВПодсистеме
метод СоздатьНастройкиДвухфакторнойАутентификации(ВторойФакторАутентификации: Массив<ВторойФакторАутентификации>): ДвухфакторнаяАутентификация
    знч НастройкиДвухфакторнойАутентификации = новый ДвухфакторнаяАутентификация()
    НастройкиДвухфакторнойАутентификации.ИспользоватьВсегда = Истина

    НастройкиДвухфакторнойАутентификации.Настройки = ВторойФакторАутентификации
    
    возврат НастройкиДвухфакторнойАутентификации
;

/**
  Описание:
    https://1cmycloud.com/console/help/element/8.1/docs/stdlib/element/Std/Users/TwoFactorAuthentication_ru/
 */
@НаСервере @ДоступноСКлиента @ВПодсистеме
метод СоздатьОписаниеПользователя(Представление: Строка, Телефон: Строка, ЭлПочта: Строка, УчетныеЗаписи: ЧитаемоеМножество<УчетнаяЗапись>): ОписаниеПользователяСервиса   
    пер ОписаниеПользователя = новый ОписаниеПользователяСервиса(СпискиПользователей.ПолучитьСписокПоУмолчанию().Ид, Представление)
        
    ОписаниеПользователя = ОписаниеПользователя
        .СЛогином(Представление)
        .СТелефоном(Телефон)
        .СЭлектроннойПочтой(ЭлПочта)
        .СУчетнымиЗаписями(УчетныеЗаписи)
        // В настройки передается массив видов факторов в порядке их приоритетности
        .СДвухфакторнойАутентификацией(СоздатьНастройкиДвухфакторнойАутентификации([ВторойФакторАутентификации.Смс(), ВторойФакторАутентификации.Почта()]))

    возврат ОписаниеПользователя
;

/**
  Описание:
    https://1cmycloud.com/console/help/element/8.1/docs/stdlib/element/Std/Users/ServiceUserDescription_ru/
 */
@НаСервере @ДоступноСКлиента @ВПодсистеме
метод СоздатьОписаниеПользователяСервиса(Представление: Строка, Телефон: Строка, ЭлПочта: Строка, УчетныеЗаписи: ЧитаемоеМножество<УчетнаяЗапись>,
    НастройкиДвухфакторнойАутентификации: ДвухфакторнаяАутентификация): ОписаниеПользователяСервиса
    
    // Создание описания пользователя в списке по умолчанию
    пер ОписаниеПользователяСервиса = новый ОписаниеПользователяСервиса(СпискиПользователей.ПолучитьСписокПоУмолчанию().Ид, Представление)
        
    ОписаниеПользователяСервиса = ОписаниеПользователяСервиса
        .СЛогином(Представление)
        .СТелефоном(Телефон)
        .СЭлектроннойПочтой(ЭлПочта)
        // У пользователя может быть несколько учетных записей, созданных в разных сервисах
        .СУчетнымиЗаписями(УчетныеЗаписи)
        .СДвухфакторнойАутентификацией(НастройкиДвухфакторнойАутентификации)

    возврат ОписаниеПользователяСервиса
;

/**
  Описание:
    https://1cmycloud.com/console/help/element/8.1/docs/stdlib/element/Std/Users/PasswordPolicy_ru/
 */
@НаСервере @ДоступноСКлиента @ВПодсистеме
метод СоздатьПолитикуПаролей(
    МинимальнаяДлина: Число = 6,
    МаксимальнаяДлина: Число = 99,
    ДолженСодержатьСимволыВВерхнемРегистре: Булево = Ложь,
    ДолженСодержатьСимволыВНижнемРегистре: Булево = Ложь,
    ДолженСодержатьСимвол: Строка = "",
    ДолженСодержатьЦифру: Булево = Ложь,
    ДопустимыйАлфавит: Строка = "",
    ЗапрещатьПредыдущиеПароли: Число = 0)
    
    знч Политика = новый ПолитикаПаролей(
        МинимальнаяДлина, 
        МаксимальнаяДлина, 
        ДолженСодержатьСимволыВВерхнемРегистре,
        ДолженСодержатьСимволыВНижнемРегистре,
        ДолженСодержатьСимвол,
        ДолженСодержатьЦифру,
        ДопустимыйАлфавит,
        ЗапрещатьПредыдущиеПароли)

    знч Список = СпискиПользователей.ПолучитьСписокПоУмолчанию()

    знч СписокСПолитикой = Список.СПолитикойПаролей(Истина, Политика)
    СпискиПользователей.Изменить(СписокСПолитикой)
;

/**
  Описание:
    https://1cmycloud.com/console/help/element/8.1/docs/stdlib/element/Std/Users/ServiceUsers_ru/
 */
@НаСервере @ДоступноСКлиента @ВПодсистеме 
метод СоздатьТокенДоступаПользователя(Ид: ИдПользователя): ДанныеЗапросаТокенаДоступа
    // Генерация токена доступа
    знч ДанныеЗапросаТокенаДоступа = ПользователиСервиса.СгенерироватьДанныеЗапросаТокенаДоступа(Ид)

    // Поиск ссылки пользователя
    знч ПользовательСсылка = Пользователи.Найти(Ид)
    
    // Разрешить доступ по токену
    Пользователи.РазрешитьДоступПоТокену(ПользовательСсылка, Истина)
        
    возврат ДанныеЗапросаТокенаДоступа
;

/**
  Описание:
    https://1cmycloud.com/console/help/element/8.1/docs/stdlib/element/Std/Users/ServiceUsers_ru/
 */
@НаСервере @ДоступноСКлиента @ВПодсистеме 
метод ИзменитьКонтактныеДанныеПользователя(Ид: ИдПользователя, НовыйТелефон: Строка, НоваяЭлПочта: Строка)
    // Поиск описания пользователя
    знч ОписаниеПользователяСервиса = ПользователиСервиса.Найти(Ид)
    
    если ОписаниеПользователяСервиса == Неопределено
        возврат
    ;
    
    // Создание копии описания пользователя с новыми контактными данными
    знч ОписаниеПользователяНовый = ОписаниеПользователяСервиса
                                        .СТелефоном(НовыйТелефон)
                                        .СЭлектроннойПочтой(НоваяЭлПочта)
    
    ПользователиСервиса.Изменить(ОписаниеПользователяНовый)
;

/**
  Описание:
    https://1cmycloud.com/console/help/element/8.1/docs/stdlib/element/Std/Users/ServiceUsers_ru/
 */
@НаСервере @ДоступноСКлиента @ВПодсистеме 
метод ПолучитьПризнакЗапретаВходаПользователя(Ид: ИдПользователя): Булево?
    // Поиск описания пользователя
    знч ОписаниеПользователя = ПользователиСервиса.Найти(Ид)
    
    если ОписаниеПользователя == Неопределено
        возврат Неопределено
    ;
    
    // Поиск ссылки на справочник по идентификатору пользователя
    знч ПользовательСсылка = Пользователи.Найти(ОписаниеПользователя.Ид)
    
    если ПользовательСсылка == Неопределено
        возврат Неопределено
    ;
    
    // Загрузка объекта справочника с блокировкой, требуется транзакция
    исп Транзакции.Начать()
    знч ПользовательОбъект = ПользовательСсылка.ЗагрузитьОбъект(Истина)
        
    возврат ПользовательОбъект == Неопределено ? Неопределено : ПользовательОбъект.ЗапрещенВход
;