@Обработчик
метод ПослеСоздания()
    Обновить.Выполнить()
;



@Обработчик
метод ПриОбновлении()
    СвойстваЗагружены = Ложь
    
    Свойства = МетодыПользователей.ЗагрузитьПользователя(Пользователь)
    
    ПоставщикиАутентификации.Данные.Очистить()
    ПоставщикиАутентификации.Данные.ДобавитьВсе(Свойства.ДанныеАутентификации)
    
    Утверждения.Данные.Очистить()
    Утверждения.Данные.ДобавитьВсе(Свойства.Утверждения)
    
    СвойстваЗагружены = Истина
;

метод ЗаписатьИзмененияОбработчик(Команда: ОбычнаяКоманда)
    знч Дубли = НайтиПовторыУтверждений()
    если не Дубли.Пусто()
        Диалог.Вопрос(
            "Утверждения содержат повторы",
            [СтандартнаяКнопкаДиалога.Ок],
            "Повторяющиеся утверждения недопустимы:\н%{Строки.Соединить(Дубли, "\н")}")
        возврат
    ;

    новый ФормаПримененияИзменений(
        СписокДействий = РассчитатьСписокДействий()
    ).ОткрытьВМодальномОкне(ОжидатьЗакрытия = Истина)

    Обновить.Выполнить()
;

метод РассчитатьСписокДействий(): ЧитаемоеСоответствие<Строка, ()->Булево>
    знч Действия = <Строка, ()->Булево>{:}
    знч СвойстваПрежние = МетодыПользователей.ЗагрузитьПользователя(Пользователь)

    если СвойстваПрежние.Администратор != Свойства.Администратор
        знч ИмяДействия = Свойства.Администратор
            ? "Сделать администратором"
            : "Снять права администратора"
        Действия.Вставить(
            ИмяДействия,
            () -> МетодыПользователей.СделатьАдминистратором(Пользователь, Свойства.Администратор))
    ;

    если СвойстваПрежние.БылУспешныйВход != Свойства.БылУспешныйВход
        знч ИмяДействия = Свойства.БылУспешныйВход
            ? "Установить признак БылУспешныйВход"
            : "Снять признак БылУспешныйВход"
        Действия.Вставить(
            ИмяДействия,
            () -> МетодыПользователей.УстановитьУспешныйВход(Пользователь, Свойства.БылУспешныйВход))
    ;

    если СвойстваПрежние.ЗапрещенВход != Свойства.ЗапрещенВход
        знч ИмяДействия = Свойства.ЗапрещенВход
            ? "Запретить вход"
            : "Разрешить вход"
        Действия.Вставить(
            ИмяДействия,
            () -> МетодыПользователей.ЗапретитьВход(Пользователь, Свойства.ЗапрещенВход))
    ;
    
    если СвойстваПрежние.РазрешенДоступПоТокену != Свойства.РазрешенДоступПоТокену
        знч ИмяДействия = Свойства.РазрешенДоступПоТокену
            ? "Запретить доступ по токену"
            : "Разрешить доступ по токену"
        Действия.Вставить(
            ИмяДействия,
            () -> МетодыПользователей.РазрешитьДоступПоТокену(Пользователь, Свойства.РазрешенДоступПоТокену))
    ;
    
    если СвойстваПрежние.ЛокальОтображения != Свойства.ЛокальОтображения
        знч ИмяДействия =
            "Локаль отображения ${СвойстваПрежние.ЛокальОтображения} -> %{Свойства.ЛокальОтображения}"
        Действия.Вставить(
            ИмяДействия,
            () -> МетодыПользователей.УстановитьЛокальОтображения(Пользователь, Свойства.ЛокальОтображения))
    ;
    
    если СвойстваПрежние.ЛокальФорматирования != Свойства.ЛокальФорматирования
        знч ИмяДействия =
            "Локаль форматирования ${СвойстваПрежние.ЛокальФорматирования} -> %{Свойства.ЛокальФорматирования}"
        Действия.Вставить(
            ИмяДействия,
            () -> МетодыПользователей.УстановитьЛокальФорматирования(Пользователь, Свойства.ЛокальФорматирования))
    ;
    
    знч ИзмененияУтверждений = РазницаМассивов(СвойстваПрежние.Утверждения, Утверждения.Данные)
    если не ИзмененияУтверждений.Пусто()
        знч Количество = ИзмененияУтверждений.Размер()
        знч ИмяДействия = "Изменение утверждений: %{Количество} шт."
        знч НовыеУтверждения = Утверждения.Данные.ВСоответствие(
            СУП -> СУП.Утверждение,
            СУП -> СУП.Значение)
        Действия.Вставить(
            ИмяДействия,
            () -> МетодыПользователей.УстановитьУтверждения(Пользователь, НовыеУтверждения)) 
    ;

    возврат {:}
;

метод СписокЛокалей(): Массив<ЭлементСпискаЗначений<Локаль?>>
    
    знч ПоВсемИзвестным = Локаль.ИзвестныеЛокали().Преобразовать( Лок ->
        новый ЭлементСпискаЗначений<Локаль?>(
            Значение = Лок,
            Представление = Лок.Представление()
        )
    )
    
    ПоВсемИзвестным.Добавить(
        новый ЭлементСпискаЗначений<Локаль?>(
            Значение = Неопределено,
            Представление = "Настройки системы"
        )
    )
    
    возврат ПоВсемИзвестным
;



метод НайтиПовторыУтверждений(): Массив<Строка>
    возврат Утверждения.Данные
        .ГруппироватьПо(СУП -> СУП.Утверждение)
        .Фильтровать(КиЗ -> КиЗ.Значение.Размер() > 1)
        .Преобразовать(КиЗ -> КиЗ.Ключ)
;


метод РазницаМассивов(Массив1: Массив<СвойстваУтвержденияПользователя>,
        Массив2: Массив<СвойстваУтвержденияПользователя>): ЧитаемоеСоответствие<СвойстваУтвержденияПользователя, Число>

    знч Счетчики = <СвойстваУтвержденияПользователя, Число>{:}
    для Элемент из Массив1
        знч КоличествоДо = Счетчики.ПолучитьИлиУмолчание(Элемент, 0)
        Счетчики.Вставить(Элемент, КоличествоДо + 1)
    ;
    для Элемент из Массив2
        знч КоличествоДо = Счетчики.ПолучитьИлиУмолчание(Элемент, 0)
        если КоличествоДо == 1
            Счетчики.Удалить(Элемент)
        иначе
            Счетчики.Вставить(Элемент, КоличествоДо - 1)
        ;
    ;
    возврат Счетчики
;