/**
  Описание:
    БесконечныйГенератор(НачальноеЗначение, ВычислительСледующего)
    Создает и возвращает новый генератор с бесконечной последовательностью
    значений, вычисляемых с помощью пользовательской функции.

  Возвращает:
    УниверсальныйГенератор
  
  Параметры:
    НачальноеЗначение - Объект? - Начальное значение последовательности. Является
      первым элементом, который будет выдан генератором при вызове метода получения
      следующего числа.
    ВычислительСледующего - Функция (Объект?) → Объект? -
      Функция, определяющая логику вычисления следующего значения. Принимает
      одно значение (предыдущее значение), возвращает следующее значение
      последовательности.
 */
@Глобально
статический метод БесконечныйГенератор(НачальноеЗначение: Объект?, ВычислительСледующего: (Объект?) -> Объект?): УниверсальныйГенератор
    возврат новый УниверсальныйГенератор(НачальноеЗначение, 0, ВычислительСледующего, Неопределено)
;

/**
  Описание:
    БесконечныйГенератор(НачальноеЗначение, ВычислительСледующего)
    Создает и возвращает новый генератор с бесконечной последовательностью
    значений, вычисляемых с помощью пользовательской функции.

  Возвращает:
    УниверсальныйГенератор
  
  Параметры:
    НачальноеЗначение - Объект? - Начальное значение последовательности. Является
      первым элементом, который будет выдан генератором при вызове метода получения
      следующего числа.
    ВычислительСледующегоСИндексом - Функция (Объект?, Число) → Объект? -
      Функция, определяющая логику вычисления следующего значения. Принимает два
      значения (предыдущее значение и номер шага). Возвращает следующее значение
      последовательности.
 */
@Глобально
статический метод БесконечныйГенератор(НачальноеЗначение: Объект?, ВычислительСледующегоСИндексом: (Объект?, Число) -> Объект?): УниверсальныйГенератор
    возврат новый УниверсальныйГенератор(НачальноеЗначение, 0, ВычислительСледующегоСИндексом, Неопределено)
;

/**
  Описание:
    КонечныйГенератор(НачальноеЗначение, ВычислительСледующего, Размер)
    Создает генератор, формирующий последовательность фиксированной длины.
    Значения вычисляются на основе заданной функции перехода от предыдущего значения.

  Возвращает:
    УниверсальныйГенератор - Новый экземпляр конечного генератора, выдающий
      заданное количество значений.
  
  Параметры:
    НачальноеЗначение - Объект? - Первое значение последовательности, с которого
      начинается генерация.
    ВычислительСледующего - Функция (Объект?) → Объект? -
      Функция, определяющая логику вычисления следующего значения. Принимает
      одно значение (предыдущее значение), возвращает следующее значение
      последовательности.
    Размер - Число - Количество элементов, которые должен сгенерировать объект.
      После достижения заданного размера генерация завершается.
 */
@Глобально
статический метод КонечныйГенератор(НачальноеЗначение: Объект?, ВычислительСледующего: (Объект?) -> Объект?, Размер: Число): УниверсальныйГенератор
    возврат новый УниверсальныйГенератор(НачальноеЗначение, 0, ВычислительСледующего, Размер)
;

/**
  Описание:
    КонечныйГенератор(НачальноеЗначение, ВычислительСледующего, Размер)
    Создает генератор, формирующий последовательность фиксированной длины.
    Значения вычисляются на основе заданной функции перехода от предыдущего значения.

  Возвращает:
    УниверсальныйГенератор - Новый экземпляр конечного генератора, выдающий
      заданное количество значений.
  
  Параметры:
    НачальноеЗначение - Объект? - Первое значение последовательности, с которого
      начинается генерация.
    ВычислительСледующегоСИндексом - Функция (Объект?, Число) → Объект? -
      Функция, определяющая логику вычисления следующего значения. Принимает два
      значения (предыдущее значение и номер шага). Возвращает следующее значение
      последовательности.
    Размер - Число - Количество элементов, которые должен сгенерировать объект.
      После достижения заданного размера генерация завершается.
 */
@Глобально
статический метод КонечныйГенератор(НачальноеЗначение: Объект?, ВычислительСледующегоСИндексом: (Объект?, Число) -> Объект?, Размер: Число): УниверсальныйГенератор
    возврат новый УниверсальныйГенератор(НачальноеЗначение, 0, ВычислительСледующегоСИндексом, Размер)
;

/**
  Описание:
    Проверяет, доступно ли следующее значение в последовательности генератора.
    Метод используется для управления итерацией по значениям генератора
    — например, в цикле при последовательной генерации элементов.
  Возвращает:
    Булево - Возвращает Истина, если генератор способен выдать следующее значение.
      Ложь, если достигнут конец последовательности (для конечного генератора).
  Примечание:
    - Для бесконечного генератора метод Следующий() всегда возвращает Истина.
 */
@Глобально
метод ЕстьСледующий(): Булево
    возврат МаксимальноеКоличество == Неопределено
        ? Истина
        : Счетчик < МаксимальноеКоличество
;

/**
  Описание:
    Возвращает следующее значение последовательности, вычисленное на основе
    пользовательской функции перехода. Метод изменяет внутреннее состояние
    генератора, обновляя текущее значение и счётчик итераций.
  Возвращает:
    Объект? - Следующее значение последовательности, рассчитанное в соответствии
      с логикой, заданной параметром ВычислительСледующего.
  Примечание:
    - При первом вызове возвращается начальное значение (НачальноеЗначение).
    - Если функция определена как (Объект?) → Объект?, то в неё передаётся только
      предыдущее значение.
    - Если функция определена как (Объект?, Число) → Объект?, то в неё передаются
      предыдущее значение и текущий индекс (счётчик итерации).
  Исключения:
    Тип исключения - ИсключениеНедопустимоеСостояние.
      Условие: Генерация следующего значения невозможна, так как достигнут
        лимит последовательности.
  Пример:  
    Ген = УниверсальныйГенератор.КонечныйГенератор(1, (n) -> n * 2, 4);
    пока Ген.ЕстьСледующий()
      Сообщить(Ген.Следующий())
    ;
    // Вывод: 1, 2, 4, 8
 */
@Глобально
метод Следующий(): Объект?
    если ЕстьСледующий()
        если Счетчик == 0
            Счетчик = 1
            возврат Значение
        ;
        выбор ВычислительСледующего
        когда это (Объект?) -> Объект?
            знч ОтПредыдущего = ВычислительСледующего как (Объект?) -> Объект?
            Значение = ОтПредыдущего(Значение)
        когда это (Объект?, Число) -> Объект?
            знч ОтПредыдущегоСИндексом = ВычислительСледующего как (Объект?,Число) -> Объект?
            Значение = ОтПредыдущегоСИндексом(Значение,Счетчик)
        ;
        Счетчик += 1
        возврат Значение
    иначе
        выбросить новый ИсключениеНедопустимоеСостояние("Достигнут лимит размера последовательности")
    ;
;

/**
  Описание:
    Преобразует конечную последовательность генератора в массив значений. Метод
    извлекает все оставшиеся значения и возвращает их в виде массива. Работает
    только для конечных генераторов.
  Возвращает:
    Массив<Объект?> - Массив, содержащий все значения последовательности от
      текущего состояния генератора до конца. Порядок элементов соответствует
      порядку генерации.
  Примечание:
    - Метод изменяет внутреннее состояние генератора: после вызова все
      значения извлечены, последующие вызовы ЕстьСледующий() вернут Ложь.
  Исключения:
    Тип исключения - ИсключениеНедопустимоеСостояние.
      Условие: Попытка преобразовать бесконечную последовательность в массив
  Пример:  
    Ген = УниверсальныйГенератор.КонечныйГенератор(1, (n) -> n * 2, 4);
    МассивЧисел = Ген.ВМассив(); // [1, 2, 4, 8]

    // После вызова ВМассив() генератор исчерпан
    Сообщить(Ген.ЕстьСледующий()); // Ложь
 */
@Глобально
метод ВМассив(): Массив<Объект?>
    если МаксимальноеКоличество != Неопределено
        знч Результат = <Объект?>[]
        
        пока ЕстьСледующий()
            Результат.Добавить(Следующий())
        ;
        
        возврат Результат
    иначе
        выбросить новый ИсключениеНедопустимоеСостояние("Невозможно создать массив для бесконечной последовательности")
    ;
;