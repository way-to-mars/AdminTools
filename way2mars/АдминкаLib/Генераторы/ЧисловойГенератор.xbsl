/**
  Описание:
    БесконечныйГенератор(НачальноеЗначение, ВычислительСледующего)
    Создает и возвращает новый числовой генератор с бесконечной последовательностью
    значений, вычисляемых с помощью пользовательской функции.

  Возвращает:
    ЧисловойГенератор
  
  Параметры:
    НачальноеЗначение - Число - Начальное значение последовательности. Является
      первым элементом, который будет выдан генератором при вызове метода получения
      следующего числа.
    ВычислительСледующего - Функция (Число) → Число -
      Функция, определяющая логику вычисления следующего значения. Принимает
      одно значение (предыдущее число), возвращает следующее число последовательности.
 */
@Глобально
статический метод БесконечныйГенератор(НачальноеЗначение: Число, ВычислительСледующего: (Число) -> Число): ЧисловойГенератор
    возврат новый ЧисловойГенератор(НачальноеЗначение, 0, ВычислительСледующего, Неопределено)
;

/**
  Описание:
    БесконечныйГенератор(НачальноеЗначение, ВычислительСледующего)
    Создает и возвращает новый числовой генератор с бесконечной последовательностью
    значений, вычисляемых с помощью пользовательской функции.

  Возвращает:
    ЧисловойГенератор
  
  Параметры:
    НачальноеЗначение - Число - Начальное значение последовательности. Является
      первым элементом, который будет выдан генератором при вызове метода получения
      следующего числа.
    ВычислительСледующегоСИндексом - Функция (Число, Число) → Число -
      Функция, определяющая логику вычисления следующего значения. Принимает два
      значения (предыдущее число и номер шага). Возвращает следующее число
      последовательности.
 */
@Глобально
статический метод БесконечныйГенератор(НачальноеЗначение: Число, ВычислительСледующегоСИндексом: (Число, Число) -> Число): ЧисловойГенератор
    возврат новый ЧисловойГенератор(НачальноеЗначение, 0, ВычислительСледующегоСИндексом, Неопределено)
;

/**
  Описание:
    КонечныйГенератор(НачальноеЗначение, ВычислительСледующего, Размер)
    Создает числовой генератор, формирующий последовательность фиксированной длины.
    Значения вычисляются на основе заданной функции перехода от предыдущего числа.

  Возвращает:
    ЧисловойГенератор - Новый экземпляр конечного числового генератора, выдающий
      заданное количество значений.
  
  Параметры:
    НачальноеЗначение - Число - Первое значение последовательности, с которого
      начинается генерация.
    ВычислительСледующего - Функция (Число) → Число -
      Функция, определяющая логику вычисления следующего значения. Принимает
      одно значение (предыдущее число), возвращает следующее число последовательности.
    Размер - Число - Количество элементов, которые должен сгенерировать объект.
      После достижения заданного размера генерация завершается.
 */
@Глобально
статический метод КонечныйГенератор(НачальноеЗначение: Число, ВычислительСледующего: (Число) -> Число, Размер: Число): ЧисловойГенератор
    возврат новый ЧисловойГенератор(НачальноеЗначение, 0, ВычислительСледующего, Размер)
;

/**
  Описание:
    КонечныйГенератор(НачальноеЗначение, ВычислительСледующего, Размер)
    Создает числовой генератор, формирующий последовательность фиксированной длины.
    Значения вычисляются на основе заданной функции перехода от предыдущего числа.

  Возвращает:
    ЧисловойГенератор - Новый экземпляр конечного числового генератора, выдающий
      заданное количество значений.
  
  Параметры:
    НачальноеЗначение - Число - Первое значение последовательности, с которого
      начинается генерация.
    ВычислительСледующегоСИндексом - Функция (Число, Число) → Число -
      Функция, определяющая логику вычисления следующего значения. Принимает два
      значения (предыдущее число и номер шага). Возвращает следующее число
      последовательности.
      После достижения заданного размера генерация завершается.
 */
@Глобально
статический метод КонечныйГенератор(НачальноеЗначение: Число, ВычислительСледующегоСИндексом: (Число, Число) -> Число, Размер: Число): ЧисловойГенератор
    возврат новый ЧисловойГенератор(НачальноеЗначение, 0, ВычислительСледующегоСИндексом, Размер)
;

/**
  Описание:
    Проверяет, доступно ли следующее значение в последовательности генератора.
    Метод используется для управления итерацией по значениям числового генератора
    — например, в цикле при последовательной генерации элементов.
  Возвращает:
    Булево - Возвращает Истина, если генератор способен выдать следующее значение.
      Ложь, если достигнут конец последовательности (для конечного генератора).
  Примечание:
    - Для бесконечного генератора метод Следующий() всегда возвращает Истина.
 */
@Глобально
метод ЕстьСледующий(): Булево
    возврат МаксимальноеКоличество == Неопределено
        ? Истина
        : Счетчик < МаксимальноеКоличество
;

/**
  Описание:
    Возвращает следующее значение последовательности, вычисленное на основе
    пользовательской функции перехода. Метод изменяет внутреннее состояние
    генератора, обновляя текущее значение и счётчик итераций.
  Возвращает:
    Число - Следующее значение последовательности, рассчитанное в соответствии
      с логикой, заданной параметром ВычислительСледующего.
  Примечание:
    - При первом вызове возвращается начальное значение (НачальноеЗначение).
    - Если функция определена как (Число) → Число, то в неё передаётся только
      предыдущее значение.
    - Если функция определена как (Число, Число) → Число, то в неё передаются
      предыдущее значение и текущий индекс (счётчик итерации).
  Исключения:
    Тип исключения - ИсключениеНедопустимоеСостояние.
      Условие: Генерация следующего значения невозможна, так как достигнут
        лимит последовательности.
  Пример:  
    Ген = ЧисловыеГенераторы.КонечныйГенератор(1, (n) -> n * 2, 4);
    пока Ген.ЕстьСледующий()
      Сообщить(Ген.Следующий())
    ;
    // Вывод: 1, 2, 4, 8
 */
@Глобально
метод Следующий(): Число
    если ЕстьСледующий()
        если Счетчик == 0
            Счетчик = 1
            возврат Значение
        ;
        выбор ВычислительСледующего
        когда это (Число) -> Число
            знч ОтПредыдущего = ВычислительСледующего как (Число) -> Число
            Значение = ОтПредыдущего(Значение)
        когда это (Число, Число) -> Число
            знч ОтПредыдущегоСИндексом = ВычислительСледующего как (Число, Число) -> Число
            Значение = ОтПредыдущегоСИндексом(Значение,Счетчик)
        ;
        Счетчик += 1
        возврат Значение
    иначе
        выбросить новый ИсключениеНедопустимоеСостояние("Достигнут лимит размера последовательности")
    ;
;

/**
  Описание:
    Преобразует конечную последовательность генератора в массив чисел. Метод
    извлекает все оставшиеся значения и возвращает их в виде массива. Работает
    только для конечных генераторов.
  Возвращает:
    Массив<Число> - Массив, содержащий все значения последовательности от
      текущего состояния генератора до конца. Порядок элементов соответствует
      порядку генерации.
  Примечание:
    - Метод изменяет внутреннее состояние генератора: после вызова все
      значения извлечены, последующие вызовы ЕстьСледующий() вернут Ложь.
  Исключения:
    Тип исключения - ИсключениеНедопустимоеСостояние.
      Условие: Попытка преобразовать бесконечную последовательность в массив
  Пример:  
    Ген = ЧисловыеГенераторы.КонечныйГенератор(1, (n) -> n * 2, 4);
    МассивЧисел = Ген.ВМассив(); // [1, 2, 4, 8]

    // После вызова ВМассив() генератор исчерпан
    Сообщить(Ген.ЕстьСледующий()); // Ложь
 */
@Глобально
метод ВМассив(): Массив<Число>
    если МаксимальноеКоличество != Неопределено
        знч Результат = <Число>[]
        
        пока ЕстьСледующий()
            Результат.Добавить(Следующий())
        ;
        
        возврат Результат
    иначе
        выбросить новый ИсключениеНедопустимоеСостояние("Невозможно создать массив для бесконечной последовательности")
    ;
;